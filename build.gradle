buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.tterrag.com/' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        //noinspection SpellCheckingInspection
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        //noinspection SpellCheckingInspection
        classpath group: 'com.wynprice.cursemaven', name: 'CurseMaven', version: '2.1.1'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.wynprice.cursemaven'

version = '1.15.2-1.1.0'
group = 'thedarkcolour' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'futuremc'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20200213-mixed-1.15.2'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                futuremc {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                futuremc {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'futuremc', '--all', '--output', file('src/generated/resources/')

            mods {
                futuremc {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = "CraftTweaker/Quark/AutoRegLib maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name = "JEI maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "Yarn Mappings"
        url = "https://maven.tterrag.com/"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.1.14'

    implementation(fg.deobf('curse.maven:biomes-o-plenty:2912265'))
    implementation(fg.deobf('curse.maven:biomes-o-plenty-source:2912266'))
    implementation(fg.deobf('mezz.jei:jei-1.15.2:6.0.0.3'))
    //implementation(fg.deobf('mezz.jei:jei-1.15.2:6.0.0.3:sources'))
    implementation(fg.deobf('com.blamejared.crafttweaker:CraftTweaker-1.15.2:6.0.0.12'))
}

jar {
    manifest {
        attributes([
                'Specification-Title': 'futuremc',
                'Specification-Vendor': 'thedarkcolour',
                'Specification-Version': '1', // We are version 1 of ourselves
                'Implementation-Title': project.name,
                'Implementation-Version': "${version}",
                'Implementation-Vendor' :'thedarkcolour',
                'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ')
        ])
    }
}

buildscript {
    repositories {
        // For early access Kotlin versions
        maven { url = "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        // Make sure to use the correct version
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4-M1"
    }
}

apply plugin: 'kotlin'

repositories {
    maven {
        name = "Kotlin Early Access"
        url = "https://dl.bintray.com/kotlin/kotlin-eap"
    }
    maven {
        name = 'kotlinforforge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
    }
}

dependencies {
    // Use the latest version of KotlinForForge
    implementation 'thedarkcolour:kotlinforforge:1.2.1'
}

compileKotlin {
    // Needed if you use Forge.kt
    // and Kotlin 1.4 language features
    kotlinOptions {
        jvmTarget = '1.8'
        languageVersion = '1.4'
        apiVersion = '1.4'
    }

    // Required to run in dev environment
    copy {
        from "$buildDir/classes/kotlin/main" into "$buildDir/classes/java/main"
    }
}